// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/releases'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        // VanillaGradle contains a custom ForgeGradle extension for SpongeCommon (see below)
        classpath 'net.minecrell:VanillaGradle:2.0.3_1'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-20160116.192356-17'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava' // Older version of Guava conflicts with ForgeGradle
        }
        classpath 'org.spongepowered:spongegradle:0.5.1'
        classpath 'org.spongepowered:mixingradle:0.3-SNAPSHOT'
    }
}

// Apply our custom ForgeGradle extension for SpongeCommon
// Compared to the regular tweaker-server plugin, SpongeCommon has all run tasks disabled
// and the Minecraft workspace defined as "provided" so they won't conflict with the implementation
apply plugin: 'net.minecrell.vanilla.server.library'

// Create task to resolve SpongeAPI revision
apply from: 'api-revision.gradle'

// Apply shared ForgeGradle configuration
ext.common = project
apply from: 'gradle/minecraft.gradle'

// Inherit SpongeCommon version from SpongeAPI
version = api.version
ext.apiVersion = version

minecraft {
    version = '1.8.9'
}

dependencies {
    compile api
    compile('org.spongepowered:mixin:0.5.3-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }

    compile 'net.sf.trove4j:trove4j:3.0.3'

    // log4j2 slf4j implementation
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'

    // Database connectors
    compile 'com.zaxxer:HikariCP:2.4.5'
    runtime 'org.mariadb.jdbc:mariadb-java-client:1.3.6'
    runtime 'com.h2database:h2:1.4.191'
    runtime 'org.xerial:sqlite-jdbc:3.8.11.2'
}

// Include API dependencies in our POM
ext.shadedDevProject = api
